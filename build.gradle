plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'checkstyle'
    alias libs.plugins.task.tree
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.gson
    implementation libs.guava
    implementation libs.logback.classic
    implementation libs.slf4j.api
    testImplementation libs.bundles.mockito.all
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.launcher
    compileOnly libs.jetbrains.annotations
}

application {
    mainClass = 'io.huangsam.trial.JavaTrial'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
    }
    // https://www.baeldung.com/jacoco-report-exclude
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'io/huangsam/**/JavaTrial.class')
        }))
    }
}
