plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'checkstyle'
    alias libs.plugins.task.tree
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.gson
    implementation libs.guava
    implementation libs.logback.classic
    implementation libs.sl4j.api
    testImplementation libs.bundles.mockito.all
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.launcher
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    // https://www.baeldung.com/jacoco-report-exclude
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'org/huangsam/sample/TrialRunner.class')
        }))
    }
}

application {
    mainClass = 'org.huangsam.sample.JavaTrial'
}
